#!/usr/bin/perl
use strict;
use warnings;

my $speedMods = "0.5x,1x,1.5x,2x,2.5x,3x,C300,C350,C400,m300";
my $defaultScreen = 'ScreenSelectMusic';
my $theme = "_fallback";
my $defaultModifiers = '"C300, midi-note, Overhead"';

my $prefs = {
  'Options' => {
    AutoMapOnJoyChange        => 0,
    OnlyDedicatedMenuButtons  => 1,
    Center1Player             => 1,
    EventMode                 => 1,
    BGBrightness              => '0.600000',
    ShowSongOptions           => 'Ask',
    ShowCaution               => 0,
    ShowInstructions          => 0,
    ShowNativeLanguage        => 0,

    Windowed                  => 1,
    DisplayWidth              => 1680,
    DisplayHeight             => 1050,
    DisplayAspectRatio        => 1.6000,
    RefreshRate               => 60,

    DisplayColorDepth         => 16,
    MaxTextureResolution      => 1024,

    LastSeenVideoDriver       => 'OpenGL',
    VideoRenderers            => 'opengl',
  },
  'Game-dance' => {
    Announcer                 => '', #MAX2
    Theme                     => $theme,
  },
};
my $themePrefs = {
  'StepMania 5 Default' => {
  }
};

my @joys = (
  'energy',
  'redoctane',
);

my $keygrid = {
  NAMES     => ['energy', 'redoctane', 'kb1',    'kb2'],
  Up        => [1,        13,          'home',   't'], 
  Down      => [2,        15,          'end',    'g'],
  Left      => [3,        16,          'delete', 'f'],
  Right     => [4,        14,          'pgdn',   'h'],
  MenuUp    => [5,        1,           'up',     'w'],
  MenuDown  => [6,        4,           'down',   's'],
  MenuLeft  => [7,        3,           'left',   'a'],
  MenuRight => [8,        2,           'right',  'd'],
  Back      => [9,        9,           'escape', 'x'],
  Start     => [10,       10,          'enter',  'z'],
};

my %jsIds = (
  energy => 'usb-ff53_USB_Gamepad-joystick',
  redoctane => 'usb-0b43_0003-joystick',
);

sub getJoysticks($$);
sub setMachineSpeedMods($);
sub generateKeymap($$$$$$$);
sub readConf($);
sub writeConf($);
sub modifyConf($$);
sub replaceConf($$);

my $usage = "Usage:\n"
  . "$0 -h|--help\n"
  . "  Print this message\n"
  . "$0 [OPTS]"
  . "  Launch stepmania after setting up preferences\n"
  . "OPTS:\n"
  . " --js1=#           override internal p1 stepmania joystick index\n"
  . " --js2=#           override internal p2 stepmania joystick index\n"
  . " -w               set Windowed to 1, overriding config\n"
  . " -f               set Windowed to 0, overriding config\n"
  . " --screen=SCREEN  jump to screen SCREEN {default=$defaultScreen}\n"
  . " -i               synonynm for --screen=ScreenTestInput\n"
;

my $smExecDir = "$ENV{HOME}/Desktop/Games/stepmania/StepMania-5.0";
my $prefsDir = "$ENV{HOME}/.stepmania-5.0/Save";
my $themesDir = "$smExecDir/Themes";
my $machineProfDir = "$prefsDir/MachineProfile";

my $prefsIni = "$prefsDir/Preferences.ini";
my $themePrefsIni = "$prefsDir/ThemePrefs.ini";
my $keymapsIni = "$prefsDir/Keymaps.ini";
my $themeMetricsIni = "$themesDir/$theme/metrics.ini";
my $speedModsFile = "$machineProfDir/SpeedMods.txt";


sub main(@){
  die $usage if @_ == 1 and $_[0] =~ /^(-h|--help)$/;

  my ($jsp1, $jsp2);
  $$prefs{Options}{TestInitialScreen} = $defaultScreen;
  for(my $i=0; $i<@_; $i++){
    if($_[$i] =~ /--js1=(\d+)/){
      $jsp1 = $1;
    }elsif($_[$i] =~ /--js2=(\d+)/){
      $jsp2 = $1;
    }elsif($_[$i] eq '-w'){
      $$prefs{Options}{Windowed} = 1;
    }elsif($_[$i] eq '-f'){
      $$prefs{Options}{Windowed} = 0;
    }elsif($_[$i] =~ /--screen=(.+)/){
      $$prefs{Options}{TestInitialScreen} = $1;
    }elsif($_[$i] eq '-i'){
      $$prefs{Options}{TestInitialScreen} = 'ScreenTestInput';
    }else{
      die $usage;
    }
  }
  my ($joy1Name, $joy1Num, $joy2Name, $joy2Num) = getJoysticks $jsp1, $jsp2;
  my ($kb1Name, $kb2Name) = ('kb1', 'kb2');

  system "mkdir", "-p", "$prefsDir";
  replaceConf $prefsIni, $prefs;
  replaceConf $themePrefsIni, $themePrefs;
  replaceConf $keymapsIni, generateKeymap(
    $keygrid, $joy1Name, $joy1Num, $joy2Name, $joy2Num, $kb1Name, $kb2Name);
  replaceConf $themeMetricsIni, {
    'Common' => {'DefaultModifiers' => $defaultModifiers}};
  setMachineSpeedMods $speedMods;
  system "$smExecDir/stepmania";
}

sub getJoysticks($$){
  my ($jsp1, $jsp2) = @_;
  my ($joy1Name, $joy1Num, $joy2Name, $joy2Num);
  for my $name(@joys){
    my $id = $jsIds{$name};
    die "Unkown joystick name: $name\n" if not defined $id;
    my $devPathById = "/dev/input/by-id/$id";
    if(-e $devPathById){
      my $devPath = `readlink $devPathById`;
      chomp $devPath;
      if($devPath =~ /js(\d+)$/){
        my $num = $1 + 1;
        if(not defined $joy1Num){
          ($joy1Name, $joy1Num) = ($name, $num);
        }elsif(not defined $joy2Num){
          ($joy2Name, $joy2Num) = ($name, $num);
        }
      }else{
        print STDERR "expected device named /dev/js##, got: $devPath\n";
      }
    }else{
      print STDERR "pad $name unplugged\n";
    }
  }
  ##### HACK FOR EMS PS2 ADAPTER #####
  if(
    defined $joy1Name and $joy1Name eq 'redoctane'
      and $joy1Num > 1 and $joy2Num != ($joy1Num-1)
  ){
    my $newNum = $joy1Num-1;
    print STDERR "HACK FOR EMS PS2 ADAPTER: redoctane $joy1Num => $newNum\n";
    $joy1Num = $newNum;
  }elsif(
    defined $joy2Name and $joy2Name eq 'redoctane'
      and $joy2Num > 1 and $joy1Num != ($joy2Num-1)
  ){
    my $newNum = $joy2Num-1;
    print STDERR "HACK FOR EMS PS2 ADAPTER: redoctane $joy2Num => $newNum\n";
    $joy2Num = $newNum;
  }
  ####################################

  if(defined $joy1Num and defined $jsp1 and $joy1Num ne $jsp1){
    print " {P1 override: $joy1Num => $jsp1}\n";
    $joy1Num = $jsp1;
  }
  if(defined $joy2Num and defined $jsp2 and $joy2Num ne $jsp2){
    print " {P2 override: $joy2Num => $jsp2}\n";
    $joy2Num = $jsp2;
  }
  if(defined $joy1Name){
    print "Player 1 Joystick: $joy1Name => $joy1Num\n";
  }
  if(defined $joy2Name){
    print "Player 2 Joystick: $joy2Name => $joy2Num\n";
  }
  return ($joy1Name, $joy1Num, $joy2Name, $joy2Num);
}

sub setMachineSpeedMods($){
  my $speedModsLine = shift;
  system "mkdir", "-p", $machineProfDir;
  open FH, "> $speedModsFile"
    or die "Couldn't write speed mods to $speedModsFile\n";
  print FH "$speedModsLine\n";
  close FH;
}

sub generateKeymap($$$$$$$){
  my ($keyGrid,
    $joy1Name, $joy1Num,
    $joy2Name, $joy2Num,
    $kb1Name, $kb2Name) = @_;
  my %namedKeymaps;

  my %buttons = map{$_ => 1} keys %$keygrid;
  delete $buttons{NAMES};

  my @orderedNames = @{$$keygrid{NAMES}};
  for(my $i=0; $i<@orderedNames; $i++){
    my $name = $orderedNames[$i];
    $namedKeymaps{$name} = {};
    for my $btn(keys %buttons){
      ${$namedKeymaps{$name}}{$btn} = ${$$keygrid{$btn}}[$i];
    }
  }
  
  my %keymap;
  for my $player((1, 2)){
    my $joyName = $player == 1 ? $joy1Name : $joy2Name;
    my $kbName = $player == 1 ? $kb1Name : $kb2Name;
    if(defined $joyName or defined $kbName){
      my $joyMap = $namedKeymaps{$joyName} if defined $joyName;
      my $kbMap = $namedKeymaps{$kbName} if defined $kbName;
      for my $btn(keys %buttons){
        my $joyBtn = $$joyMap{$btn} if defined $joyMap;
        my $kbBtn = $$kbMap{$btn} if defined $kbMap;
        my $joyNum = $player == 1 ? $joy1Num : $joy2Num;
        my $val = '';
        $val .= "Joy${joyNum}_B$joyBtn" if defined $joyBtn;
        $val .= ":" if defined $joyBtn and $kbBtn;
        $val .= "Key_$kbBtn" if defined $kbBtn;
        $keymap{"${player}_$btn"} = $val;
      }
    }
  }
  return {'dance' => \%keymap};
}

sub readConf($){
  my $s = shift();
  my $conf = {};
  my $confSection;
  for my $line(split /[\n\r]+/, $s){
    if($line =~ /^\[(.*)\]$/){
      $confSection = {};
      ${$conf}{$1} = $confSection;
    }elsif($line =~ /^(.*)=(.*)$/){
      $$confSection{$1} = $2;
    }
  }
  return $conf;
}

sub writeConf($){
  my %conf = %{shift()};
  my $s = '';
  for my $section(sort keys %conf){
    $s .= "[$section]\n";
    my $sectionConf = $conf{$section};
    for my $key(sort keys %$sectionConf){
      $s .= "$key=${$sectionConf}{$key}\n";
    }
    $s .= "\n";
  }
  return $s;
}

sub modifyConf($$){
  my %orig = %{shift()};
  my %changes = %{shift()};
  for my $section(keys %changes){
    if(not defined $orig{$section}){
      $orig{$section} = $changes{$section};
    }else{
      my $origSectionConf = $orig{$section};
      my $changesSectionConf = $changes{$section};
      for my $key(keys %$changesSectionConf){
        ${$origSectionConf}{$key} = ${$changesSectionConf}{$key};
      }
    }
  }
  return \%orig;
}

sub replaceConf($$){
  my $file = shift;
  my $changes = shift;
  my $conf = readConf `cat $file`;
  my $content = writeConf(modifyConf($conf, $changes));
  open FH, "> $file" or die "Couldnt open $file for writing.\n";
  print FH $content;
  close FH;
}

main(@ARGV);
